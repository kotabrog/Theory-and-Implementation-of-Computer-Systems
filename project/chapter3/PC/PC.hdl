// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=dffOut, out=incOut);
    Mux16(a=dffOut, b=incOut, sel=inc, out=out0);
    Mux16(a=out0, b=in, sel=load, out=out1);
    Mux16(a=out1, b[0..15]=false, sel=reset, out=out2);
    Or(a=load, b=inc, out=load0);
    Or(a=load0, b=reset, out=load1);
    Register(in=out2, load=load1, out=out, out=dffOut);
}
